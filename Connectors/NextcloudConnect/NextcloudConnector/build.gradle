plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'com.google.devtools.ksp'
}

android {
    namespace 'com.shareconnect.nextcloudconnect'
    compileSdk 36

    defaultConfig {
        applicationId 'com.shareconnect.nextcloudconnect'
        minSdk 28  // Required for SecurityAccess library compatibility
        targetSdk 36
        versionCode 1
        versionName '1.0.0'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            shrinkResources false
            applicationIdSuffix '.debug'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += ['-opt-in=kotlin.time.ExperimentalTime', '-opt-in=kotlin.ExperimentalStdlibApi']
    }

    buildFeatures {
        compose true
    }

    packaging {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/license.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/notice.txt', 'META-INF/INDEX.LIST', 'META-INF/versions/9/OSGI-INF/MANIFEST.MF']
        }
    }

    lint {
        disable 'MissingTranslation', 'ExtraTranslation', 'NullSafeMutableLiveData'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                it.jvmArgs '-XX:+AllowRedefinitionToAddDeleteMethods'
            }
        }
    }

    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }
}

tasks.withType(Test) {
    useJUnit()
    javaLauncher.set(javaToolchains.launcherFor {
        languageVersion.set(JavaLanguageVersion.of(17))
    })
}

dependencies {
    // Centralized dependencies
    implementation(project(":Dependencies"))
    implementation project(':Asinka:asinka')

    // ShareConnect integration modules
    implementation project(':DesignSystem')
    implementation project(':Onboarding')
    implementation project(':Localizations')

    // Sync modules
    implementation project(':ThemeSync')
    implementation project(':ProfileSync')
    implementation project(':HistorySync')
    implementation project(':RSSSync')
    implementation project(':BookmarkSync')
    implementation project(':PreferencesSync')
    implementation project(':LanguageSync')
    implementation project(':TorrentSharingSync')

    // Security
    implementation project(':Toolkit:SecurityAccess')

    def androidxLifecycleVersion = '2.9.4'
    def composeNavigationVersion = '2.9.0-beta03'
    def coroutinesVersion = '1.10.2'
    def kotlinxSerializationVersion = '1.7.3'
    def kotlinxDatetimeVersion = '0.6.1'
    def roomVersion = '2.7.0-alpha07'

    def ktorVersion = '3.2.3'
    def okhttpVersion = '4.12.0'
    def retrofitVersion = '2.11.0'
    def coilVersion = '2.7.0'

    implementation platform('androidx.compose:compose-bom:2025.09.00')
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    implementation 'androidx.compose.runtime:runtime'
    implementation 'androidx.compose.foundation:foundation'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation "org.jetbrains.androidx.lifecycle:lifecycle-viewmodel:$androidxLifecycleVersion"
    implementation "org.jetbrains.androidx.lifecycle:lifecycle-runtime-compose:$androidxLifecycleVersion"
    implementation 'androidx.compose.material:material-icons-extended'

    implementation 'androidx.activity:activity-compose:1.11.0'
    implementation 'androidx.compose.material3:material3-window-size-class'

    implementation "org.jetbrains.androidx.navigation:navigation-compose:$composeNavigationVersion"


    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinxSerializationVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-datetime:$kotlinxDatetimeVersion"

    // Room Database
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    ksp "androidx.room:room-compiler:$roomVersion"

    // Retrofit for Nextcloud API
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"

    implementation project.dependencies.platform("com.squareup.okhttp3:okhttp-bom:$okhttpVersion")
    implementation 'com.squareup.okhttp3:okhttp'
    implementation 'com.squareup.okhttp3:logging-interceptor'

    implementation "io.coil-kt:coil-compose:$coilVersion"

    implementation 'androidx.datastore:datastore-preferences:1.1.1'
    implementation 'androidx.startup:startup-runtime:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'

    // Test dependencies
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.13.8'
    testImplementation "androidx.room:room-testing:$roomVersion"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttpVersion"
    testImplementation 'org.robolectric:robolectric:4.13'
    testImplementation 'androidx.test:core:1.7.0'

    // Android test dependencies
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.7.7'
    debugImplementation 'androidx.compose.ui:ui-test-manifest:1.7.7'

    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okhttpVersion"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
}

// Force Kotlin version to avoid conflicts
configurations.all {
    resolutionStrategy {
        force 'org.jetbrains.kotlin:kotlin-stdlib:2.0.0'
        force 'org.jetbrains.kotlin:kotlin-reflect:2.0.0'
        force 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:2.0.0'
        force 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.0.0'
        force 'org.jetbrains.kotlin:kotlin-stdlib-common:2.0.0'
    }
}
